def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY')
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
	apply plugin: 'com.android.application'
} else {
	apply plugin: 'com.android.library'
}

android {
	// ndkVersion '26.1.10909125' - asio is not ready for this
	ndkVersion '25.2.9519653'
	compileSdk 35
	aaptOptions { // probably does not matter...
		noCompress 'mpq'
	}
	// if (buildAsApplication) {
	namespace 'org.diasurgical.devilutionx'
	// }
	defaultConfig {
		if (buildAsApplication) {
			applicationId "org.diasurgical.devilx"
		}
		minSdkVersion 21
		targetSdkVersion 35
		versionCode 666
		versionName "1.0.0"
		externalNativeBuild {
			cmake {
				arguments "-DANDROID_STL=c++_static", project.findProperty("cmakearg0"), project.findProperty("cmakearg1"), project.findProperty("cmakearg2")
				abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
			}
		}
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	buildFeatures {
		buildConfig true
	}
	applicationVariants.all { variant ->
		tasks["merge${variant.name.capitalize()}Assets"]
			.dependsOn("externalNativeBuild${variant.name.capitalize()}")
	}
	if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
		sourceSets.main {
			jniLibs.srcDir 'libs'
		}
		externalNativeBuild {
			cmake {
				path '../../CMakeLists.txt'
				version "3.13.0+"
				buildStagingDirectory System.env.DevilutionX_Android_StagingDirectory
			}
		}

	}

	if (buildAsLibrary) {
		libraryVariants.all { variant ->
			variant.outputs.each { output ->
				def outputFile = output.outputFile
				if (outputFile != null && outputFile.name.endsWith(".aar")) {
					def fileName = "org.diasurgical.devilutionx.aar"
					output.outputFile = new File(outputFile.parent, fileName)
				}
			}
		}
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'com.android.support.constraint:constraint-layout:2.0.4'
}
